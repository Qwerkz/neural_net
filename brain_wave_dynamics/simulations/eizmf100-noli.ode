# f(x)=(a/pi)*sqrt(x/(1-exp(-x/sigma)))
f(x)=(a/pi)*sqrt(log(1+exp(x/sigma))*sigma)
table we % 51 -25 25 exp(-abs(t)/le)/(2*le)
special sehat=conv(even,100,25,we,se1)
par gL=.05, vT=-55, vR=-65, vEX=0, vK=-85, vIN=-70, Ie=0,Ii=0
par  gie=1.2,gii=.1, gee=1.5,gei=.5, ga=0.2
par le=5
par tstart=40.0001
par thr=.05
aux g_ie=gie
global 1 t-tstart {se1=.1}
global 1 se40-thr {t40=t}
global 1 se60-thr {t60=t;vel=20/max(.01,t60-t40)} 
ge_ex[1..100]=gee*sehat([j-1])
ge_in[1..100]=gie*si[j]
gi_ex[1..100]=gei*sehat([j-1])
gi_in[1..100]=gii*si[j]
g_ahp[1..100]=ga*z[j]
par sigma=5.7,tau_se=3,tau_si=4,tau_z=80
a=gL/(vT-vR)
be[1..100]=-a*vT-a*vR-ge_ex[j]-ge_in[j]-g_ahp[j]
bi[1..100]=-a*vT-a*vR-gi_ex[j]-gi_in[j]
ce[1..100]=a*vR*vT+ge_ex[j]*vEX+ge_in[j]*vIN+g_ahp[j]*vK+Ie
ci[1..100]=a*vR*vT+gi_ex[j]*vEX+gi_in[j]*vIN+Ii
betae[1..100]=ce[j]/a-be[j]^2/(4*a^2)
betai[1..100]=ci[j]/a-bi[j]^2/(4*a^2)
fe[1..100]=f(betae[j])
fi[1..100]=f(betai[j])
se[1..100]'=-se[j]/tau_se+fe[j]
si[1..100]'=-si[j]/tau_si+fi[j]
z[1..100]'=-z[j]/tau_z+fe[j]
t40'=0
vel'=0
t60'=0
init se[1..100]=.00046,si[j]=.0019,z[j]=.015
@ total=200,nout=4,bound=100000
@ xhi=100,ylo=0,yhi=.15
@ yp=se40
done
